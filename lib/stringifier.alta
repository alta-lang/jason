import "classes.alta" as jason
import String from "string"
import Size from "types"
import floatingPointToString from "util"
import escapeString from "util.alta"

export function stringify(item: ptr jason.Jason): String {
  let result = new String
  let json: ref jason.Jason = *item
  if json instanceof jason.Null {
    result += "null"
  } else if json instanceof jason.Array {
    result += "["
    let arr = json as ref jason.Array
    let isFirst = true
    for i: Size in 0..arr.length {
      if isFirst {
        isFirst = false
      } else {
        result += ","
      }
      result += stringify(arr[i])
    }
    result += "]"
  } else if json instanceof jason.Object {
    result += "{"
    let obj = json as ref jason.Object
    let keys = obj.getKeys()
    let isFirst = true
    for i: Size in 0..keys.length {
      if isFirst {
        isFirst = false
      } else {
        result += ","
      }
      let key = keys[i]
      result += "\""
      result += escapeString(key)
      result += "\""
      result += ":"
      result += stringify(obj[key])
    }
    result += "}"
  } else if json instanceof jason.String {
    let str = json as ref jason.String
    result += "\""
    result += escapeString(str)
    result += "\""
  } else if json instanceof jason.Number {
    let num = json as ref jason.Number
    result += floatingPointToString<double>(num.value)
  } else if json instanceof jason.Boolean {
    let bool = json as ref jason.Boolean
    if bool.value {
      result += "true"
    } else {
      result += "false"
    }
  }
  return result
}
